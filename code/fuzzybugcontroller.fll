#File: fuzzybugcontroller.fll

Engine: ObstacleAvoidance

InputVariable: ObstacleDirection
  enabled: true
  range: -1.6 1.6
  lock-range: false
  term: right Ramp 0 -1.57
  term: center Triangle -0.785 0.000 0.785
  term: left Ramp 0 1.57

InputVariable: ObstacleFree
  enabled: true
  range: -1.6 1.6
  lock-range: false
  term: right Ramp 0 -1.57
  term: center Triangle -0.785 0.000 0.785
  term: left Ramp 0 1.57

InputVariable: ObstacleDistance
  enabled: true
  range: 0 10
  lock-range: false
  term: veryclose Ramp 1.2 0
  term: close Triangle 1 1.2 1.4
  term: far Ramp 1.2 2.5

InputVariable: MarbleDirection
  enabled: true
  range: -200 200
  lock-range: false
  term: marbleRight Ramp -1 -20
  term: marbleCenter Triangle -5 0 5
  term: marbleLeft Ramp 1 20

InputVariable: MarbleFound
  enabled: true
  range: 0 100
  lock-range: false
  term: no Ramp 8 0
  term: yes Ramp 5 30

InputVariable: GoalDirection
  enabled: true
  range: -3.14 3.14
  lock-range: false
  term: goalLeft Ramp 0 -0.7
  term: goalStraight Triangle -0.5 0 0.5
  term: goalRight Ramp 0 0.7

InputVariable: BoundaryDirection
  enabled: true
  range: -3.14 3.14
  lock-range: false
  term: boundLeft Ramp 0 -0.7
  term: boundStraight Triangle -0.5 0 0.5
  term: boundRight Ramp 0 0.7

OutputVariable: SteerDirection
  enabled: true
  range: -1.57 1.57
  lock-range: false
  aggregation: Maximum
  defuzzifier: Centroid 100
  default: 0
  lock-previous: false
  term: sharpright Ramp -1 -1.57
  term: right      Triangle -1.57 -1.3 -1
  term: softright  Triangle -1.3 -1.1 -0.9
  term: straight   Triangle -0.6 0 0.6
  term: softleft   Triangle 0.9 1.1 1.2
  term: left       Triangle 1 1.3 1.57
  term: sharpleft  Ramp  1 1.57

OutputVariable: Speed
  enabled: true
  range: -5 5
  lock-range: false
  aggregation: Maximum
  defuzzifier: Centroid 100
  default: 0
  lock-previous: false
  term: backward Ramp  -0.75 -2
  term: softbackward Triangle -1 -0.5 0.000
  term: softforward Triangle 0.000 0.5 1
  term: forward Ramp  -2 4


RuleBlock: mamdani
  enabled: true
  conjunction: Minimum
  disjunction: Maximum
  implication: Minimum
  activation: General

  rule: if ObstacleDistance is veryclose and ObstacleDirection is left and MarbleFound is no then SteerDirection is softleft
  rule: if ObstacleDistance is veryclose and ObstacleDirection is right and MarbleFound is no then SteerDirection is softright
  rule: if ObstacleDistance is veryclose and ObstacleDirection is center and ObstacleFree is left and MarbleFound is no then SteerDirection is softright
  rule: if ObstacleDistance is veryclose and ObstacleDirection is center and ObstacleFree is right and MarbleFound is no then SteerDirection is softleft
  rule: if ObstacleDistance is veryclose and MarbleFound is no then Speed is forward

  rule: if ObstacleDistance is close and MarbleFound is no and BoundaryDirection is boundLeft and GoalDirection is goalLeft then SteerDirection is softleft
  rule: if ObstacleDistance is close and MarbleFound is no and BoundaryDirection is boundRight and GoalDirection is goalRight then SteerDirection is softright
  rule: if ObstacleDistance is close and MarbleFound is no and BoundaryDirection is boundLeft and GoalDirection is goalRight then SteerDirection is softright
  rule: if ObstacleDistance is close and MarbleFound is no and BoundaryDirection is boundRight and GoalDirection is goalLeft then SteerDirection is softleft
  rule: if ObstacleDistance is close and MarbleFound is no and BoundaryDirection is boundStraight then SteerDirection is straight
  rule: if ObstacleDistance is close and MarbleFound is no then Speed is forward

  rule: if ObstacleDistance is far and MarbleFound is no and GoalDirection is goalRight then SteerDirection is right
  rule: if ObstacleDistance is far and MarbleFound is no and GoalDirection is goalLeft then SteerDirection is left
  rule: if ObstacleDistance is far and MarbleFound is no and GoalDirection is goalStraight then SteerDirection is straight
  rule: if ObstacleDistance is far and MarbleFound is no and GoalDirection is goalStraight then Speed is forward

  rule: if MarbleFound is yes and MarbleDirection is marbleLeft then SteerDirection is softleft
  rule: if MarbleFound is yes and MarbleDirection is marbleRight then SteerDirection is softright
  rule: if MarbleFound is yes and MarbleDirection is marbleCenter then Speed is forward

